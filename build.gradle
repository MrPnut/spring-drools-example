allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    version = '1.0'
    group = 'com.ncsoftworks'

    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            url 'https://repository.jboss.org/nexus/content/groups/public'
        }
    }
}

subprojects {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    jar {
        into("META-INF/maven/$project.group/$project.name") {
            from "$project.buildDir/publications/mavenJava"
            include "*.pom"
            include "*.xml"
        }
        into("META-INF/maven/$project.group/$project.name") {
            from "$project.buildDir/publications/mavenJava"
            include "*.properties"
        }
    }

    task pomProperties << {
        Properties props = new Properties();
        File propertiesFile = new File("$project.buildDir/publications/mavenJava/pom.properties")
        if (!propertiesFile.exists()) {
            propertiesFile.createNewFile()
        }
        props.load(propertiesFile.newDataInputStream())
        props.setProperty('version', project.version)
        props.setProperty('groupId', project.group)
        props.setProperty('artifactId', project.name)
        props.store(propertiesFile.newWriter(), null)
    }

    project.afterEvaluate {
        project.tasks.generatePomFileForMavenJavaPublication.dependsOn pomProperties
        project.tasks.jar.dependsOn generatePomFileForMavenJavaPublication
    }
}
